* energy minimization using the HyRes model 
* and calculate RMSD, energy and Rg after EM
*

if @?pdbid eq 0 stop
if @?segid eq 0 set segid = p53

set toppar = required
set inpdir = ../s4-mkcg

!HyRes2 force fields 
read  rtf card name @toppar/top-idpcg-sasa.inp
read para card name @toppar/param-idpcg-sasa.inp

open read card unit 15 name @inpdir/@pdbid.pdb
read sequ pdb  unit 15

bomlev -1
gene @segid first none last none setup
bomlev 0

open writ card unit 10 name ../s5-cgem/@pdbid.psf
writ psf  card unit 10
* psf of @pdbid for cg system
*

open read card unit 15 name @inpdir/@pdbid.pdb
read coor pdb  unit 15
clos unit 15

coor copy comp

! define backbone
define bb sele type CA .or. type N .or. type H .or. type C .or. type O end
define heavyatoms sele all end

faster on
cons harm clear
cons harm force 0.5  sele heavyatoms end

! ionc 0.15: for DH screening
NBOND atom switch cdie vdw vswitch -
      cutnb 20.0 ctofnb 18.0 ctonnb 16.0 -
      eps 20 ionc 0.15

energy

nbond inbfrq -1
hbond ihbfrq -1

shake tolerance 1.0e-06 bonh param

open writ card unit 10 name ../s5-cgem/@pdbid.rmsd.dat
writ titl unit 10
*#steps overall-rmsd bb-rmsd sc-rmsd-align-bb sc-rmsd-align-sc energy rg
*

coor orient rms sele all end
set rmsd1 = ?rms
coor orient rms sele bb end
set rmsd2 = ?rms
coor rms  sele .not. bb end
set rmsd3 = ?rms
coor orient rms sele .not. bb end
set rmsd4 = ?rms
energy
coor rgyr sele all end
writ titl unit 10
*0 @rmsd1 @rmsd2 @rmsd3 @rmsd4 ?ener ?rgyr
*

set stepsize = 40
set inx = 1
label nextem1

      calc step = @inx * @stepsize
      calc fc = 10.0
      ! energy minimization
      cons harm clear
      cons harm force @fc sele bb end
      mini   sd  nstep @stepsize nprint 20 step 0.005
      mini abnr  nstep @stepsize nprint 20 step 0.005

      coor orient rms sele all end
      set rmsd1 = ?rms
      coor orient rms sele bb end
      set rmsd2 = ?rms
      coor rms  sele .not. bb end
      set rmsd3 = ?rms
      coor orient rms sele .not. bb end
      set rmsd4 = ?rms
      energy
      coor rgyr sele all end
      writ titl unit 10
      *@step @rmsd1 @rmsd2 @rmsd3 @rmsd4 ?ener ?rgyr
      *

      incr inx by 1
if inx le 20 goto nextem1

open writ card unit 15 name ../s5-cgem/@pdbid.mini1.pdb
writ coor pdb  unit 15
* @pdbid after energy minimization1
*

label nextem2

      calc step = @inx * @stepsize
      ! energy minimization
      cons harm clear
      mini   sd  nstep @stepsize nprint 20 step 0.005
      mini abnr  nstep @stepsize nprint 20 step 0.005

      coor orient rms sele all end
      set rmsd1 = ?rms
      coor orient rms sele bb end
      set rmsd2 = ?rms
      coor rms  sele .not. bb end
      set rmsd3 = ?rms
      coor orient rms sele .not. bb end
      set rmsd4 = ?rms
      energy
      coor rgyr sele all end
      writ titl unit 10
      *@step @rmsd1 @rmsd2 @rmsd3 @rmsd4 ?ener ?rgyr
      *

      incr inx by 1
if inx le 100 goto nextem2

open writ card unit 15 name ../s5-cgem/@pdbid.mini2.pdb
writ coor pdb  unit 15
* @pdbid after energy minimization2
*

stop
